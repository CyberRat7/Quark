plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.6-SNAPSHOT" apply false
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        maven { // Parchment mappings
            url = "https://maven.parchmentmc.org"
            content {
                includeGroup "org.parchmentmc.data"
            }
        }
        maven { // Quilt Mappings
            url = "https://maven.quiltmc.org/repository/release"
            content {
                includeGroup "org.quiltmc"
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:$minecraft_version"

        mappings(loom.layered {
            mappings("org.quiltmc:quilt-mappings:$minecraft_version+build.$qm_version:intermediary-v2")
            parchment("org.parchmentmc.data:parchment-$minecraft_version:$parchment_version@zip")
            officialMojangMappings { nameSyntheticMembers = false }
        })
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = archives_base_name
    version = mod_version
    group = maven_group

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release.set(17)
    }

    java {
        withSourcesJar()
    }
}
